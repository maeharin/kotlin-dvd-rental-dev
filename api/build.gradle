buildscript {
	ext {
		kotlinVersion = '1.1.60'
		springBootVersion = '1.5.8.RELEASE'
		springBootDomaVersion = '1.1.1'
		springSecurityOauth2Version = '2.2.0.RELEASE'
		domaVersion = '2.18.0'
		postgresqlVersion = '42.1.4'
		domaGenVersion = '2.18.0'
		elasticsearchVersion = '5.6.1'
		springfoxSwaggerVersion = '2.7.0'
		swaggerCoreVersion = '1.5.16'
	}
	repositories {
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
	}
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'com.maeharin'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
	kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
	kotlinOptions.jvmTarget = "1.8"
}

repositories {
	jcenter()
}

configurations {
	domaGenRuntime
}

// -------------------------------------------
// for Doma2 with gradle
// see: http://doma.readthedocs.io/ja/stable/build/#gradle
// -------------------------------------------
processResources.destinationDir = compileJava.destinationDir
compileJava.dependsOn processResources

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile("org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}")
	compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
	runtime('org.springframework.boot:spring-boot-devtools')
	testCompile('org.springframework.boot:spring-boot-starter-test')

	// -------------------------------------------
	// spring security
	// -------------------------------------------
	compile('org.springframework.boot:spring-boot-starter-security')
	compile "org.springframework.security.oauth:spring-security-oauth2:${springSecurityOauth2Version}"
	testCompile('org.springframework.security:spring-security-test')

	// -------------------------------------------
	// jackson
	// -------------------------------------------
	// kotlinでjacksonを扱いやすくする
	compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.0"

	// jsr310(LocalDateTimeなど）をjacksonで扱えるようにする
	// see: http://blog.rakugakibox.net/entry/2015/12/04/springboot-with-java8time
	compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.9'


	// -------------------------------------------
	// dababase
	// -------------------------------------------
	compile "org.postgresql:postgresql:${postgresqlVersion}"

	// -------------------------------------------
	// Doma2(ORM)
	// -------------------------------------------
	compile "org.seasar.doma.boot:doma-spring-boot-starter:${springBootDomaVersion}"
	compile "org.seasar.doma:doma:${domaVersion}"

	// -------------------------------------------
	// elasticsearch
	// -------------------------------------------
	compile "org.elasticsearch:elasticsearch:${elasticsearchVersion}"
	compile "org.elasticsearch.client:elasticsearch-rest-high-level-client:${elasticsearchVersion}"

	// -------------------------------------------
	// Swagger(APIドキュメント生成）
	// -------------------------------------------
	compile "io.springfox:springfox-swagger2:${springfoxSwaggerVersion}"
	compile "io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}"
	compile "io.swagger:swagger-core:${swaggerCoreVersion}"

	// -------------------------------------------
	// Doma2 gen
	// -------------------------------------------
	domaGenRuntime "org.seasar.doma:doma-gen:${domaGenVersion}"
	domaGenRuntime "org.postgresql:postgresql:${postgresqlVersion}"
}


//
// Doma-Gen
// DBのスキーマからDomaのEntity, Dao, SQLファイルを自動生成するタスク
// see: http://doma-gen.readthedocs.io/ja/stable/gen/
//
task domagen {
	doLast {
		def dbHost = System.getenv('DB_HOST')
		def dbName = System.getenv('DB_NAME')
		def dbUser = System.getenv('DB_USER')
		def dbPassword = System.getenv('DB_PASSWORD')
		def dbUrl = "jdbc:postgresql://${dbHost}/${dbName}"

		ant.taskdef(resource: 'domagentask.properties',
					classpath: configurations.domaGenRuntime.asPath)

		def packageName = "com.maeharin.kotlindvdrental.infrastructure.doma"

		ant.gen(
			url: dbUrl,
			user: dbUser,
			password: dbPassword,
			templatePrimaryDir: 'src/main/resources/doma-gen-custom-templates'
		) {
			entityConfig(
				packageName: "${packageName}.entity",
				entitySuffix: "Entity",
				overwrite: true,
				namingType: "snake_lower_case",
				useAccessor: false,
				useListener: false,
				showColumnName: false,
				showDbComment: true
			)
			daoConfig(packageName: "${packageName}.dao")
			sqlConfig()
		}
	}
}

//
// swagger codegenのjarを取得しswagger-codegenディレクトリにコピーする
//
task copySwaggerCodegen(type: Copy) {
    configurations {
        swaggerCodegen
    }

    dependencies {
        swaggerCodegen "io.swagger:swagger-codegen-cli:2.2.3"
    }

    from configurations.swaggerCodegen

    into 'swagger-codegen'
}

task swaggerCodeGen(type: Exec, dependsOn: copySwaggerCodegen) {
	workingDir './swagger-codegen'
	commandLine './generate_client.sh'
}

build.finalizedBy(swaggerCodeGen)
